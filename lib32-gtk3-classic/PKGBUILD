# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Tomasz Gąsior <tomaszgasior.pl>

_repo=gtk3-mushrooms
_commit=cda405622587943ea27ebe3df83c5c874d6654b3

pkgname=lib32-gtk3-classic
pkgver=3.22.25
pkgrel=1
pkgdesc="GTK3 library modified for classic GTK-based desktops."
url="https://github.com/TomaszGasior/gtk3-mushrooms"
conflicts=(lib32-gtk3)
provides=("lib32-gtk3=$pkgver")
arch=(x86_64)
license=(LGPL)
depends=(
    gtk3-classic
#    lib32-atk
    lib32-at-spi2-atk
#    lib32-cairo
    lib32-gdk-pixbuf2
    lib32-json-glib
    lib32-libcups
    lib32-libepoxy
    lib32-librsvg
    lib32-libxcomposite
    lib32-libxcursor
#    lib32-libxdamage
#    lib32-libxi
    lib32-libxinerama
    lib32-libxkbcommon
    lib32-libxrandr
#    lib32-mesa
    lib32-pango
    lib32-rest
#    lib32-wayland
    )
optdepends=(
    'gtk3-print-backends: printing'
    'dconf: default GSettings backend'
    'adwaita-icon-theme: default icon theme'
    'cantarell-fonts: default font'
    )
makedepends=(
    'git'
    'gcc-multilib'
    'gobject-introspection'
    'gtk-doc'
    )
source=(
    # GTK source code.
    "https://github.com/GNOME/gtk/archive/$pkgver.tar.gz"
    # ArchLinux hook.
    'gtk-query-immodules-3.0-32.hook'
    # Fix Makefile mistake — https://bugzilla.gnome.org/show_bug.cgi?id=789630.
    "__fix-3.22.25-mistake.__patch::https://bug789630.bugzilla-attachments.gnome.org/attachment.cgi?id=362496"
    # gtk3-mushrooms patches
    "git+$url.git#commit=$_commit"
)
sha256sums=('d72da94dcd208a8a76ad156247e3453bde308555e767c41a303ffc28015b68bd'
            'c87e56504cd38d73748d12e87353680156cdeef30d082cca407d2375db821115'
            '37e6b2669c055f34a3264b71f748b08465de225f32ed4e1453deea12eafc0c05'
            'SKIP')

__patch_makefiles()
{
	__replace_string_in_file()
	{
		sed -i".bak" "s/$1/$2/" "$3"
		rm "$3.bak"
	}

	__replace_string_in_file \
		"SRC_SUBDIRS = gdk gtk libgail-util modules demos tests testsuite examples" \
		"SRC_SUBDIRS = gdk gtk libgail-util modules demos" \
		"Makefile.am"

	__replace_string_in_file \
		"SUBDIRS = po po-properties \$(SRC_SUBDIRS) docs m4macros build" \
		"SUBDIRS = po \$(SRC_SUBDIRS) m4macros build" \
		"Makefile.am"

	__replace_string_in_file \
		"SUBDIRS = gtk-demo widget-factory icon-browser" \
		"SUBDIRS = widget-factory" \
		"demos/Makefile.am"

	__replace_string_in_file "SUBDIRS += cloudprint" "" "modules/printbackends/Makefile.am"
	__replace_string_in_file "SUBDIRS += cups" 	     "" "modules/printbackends/Makefile.am"
	__replace_string_in_file "gtk-update-icon-cache" "" "gtk/Makefile.am"
}

__patch_gtk_code()
{
	for patch_file in $srcdir/$_repo/*.patch; do
		patch -d "gtk" -p 3 -i "$patch_file"
	done

	cat "$srcdir/$_repo/smaller-adwaita.css" | tee -a gtk/theme/Adwaita/gtk-contained{,-dark}.css > /dev/null
}

prepare() {
    cd gtk-$pkgver

    # Fix Makefile mistake — https://bugzilla.gnome.org/show_bug.cgi?id=789630.
    patch -p 1 -i "$srcdir/__fix-3.22.25-mistake.__patch"

    # Make building faster by skipping tests, code examples and unused elements.
    __patch_makefiles

    # Apply patches to GTK code.
    __patch_gtk_code
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd gtk-$pkgver

    export CC='gcc -m32'
    export CXX='/bin/false'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    CXX=/bin/false ./configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-x11-backend \
      --enable-wayland-backend \
      --disable-schemas-compile \
      --disable-gtk-doc-html \
      --disable-libcanberra

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd gtk-$pkgver

    make DESTDIR="$pkgdir" install
    mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
    mv "${pkgdir}"/usr/bin/gtk-query-settings{,-32}
    rm "${pkgdir}"/usr/bin/{gtk-{builder-tool,encode-symbolic-svg,launch},gtk3-widget-factory}
    rm -rf "${pkgdir}"/{etc,usr/{include,share}}

    install -Dm 644 ../gtk-query-immodules-3.0-32.hook "${pkgdir}"/usr/share/libalpm/hooks/gtk-query-immodules-3.0-32.hook
}
