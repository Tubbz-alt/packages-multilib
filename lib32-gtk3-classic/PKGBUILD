# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Tomasz GÄ…sior <tomaszgasior.pl>

_repo=gtk3-mushrooms
_commit=0c2a04f3237639f8da136f934548be987f344836
__arch_pkg_commit="712b5a879d9389efba706c8817607fa65ac562e9"

pkgname=lib32-gtk3-classic
pkgver=3.22.24
pkgrel=1
pkgdesc="GTK3 library modified for classic GTK-based desktops."
url="https://github.com/TomaszGasior/gtk3-mushrooms"
conflicts=(lib32-gtk3)
provides=("lib32-gtk3=$pkgver")
arch=(x86_64)
license=(LGPL)
depends=(
    gtk3-classic
#    lib32-atk
    lib32-at-spi2-atk
#    lib32-cairo
    lib32-gdk-pixbuf2
    lib32-json-glib
    lib32-libcups
    lib32-libepoxy
    lib32-librsvg
    lib32-libxcomposite
    lib32-libxcursor
#    lib32-libxdamage
#    lib32-libxi
    lib32-libxinerama
    lib32-libxkbcommon
    lib32-libxrandr
#    lib32-mesa
    lib32-pango
    lib32-rest
#    lib32-wayland
    )
optdepends=(
    'gtk3-print-backends: printing'
    'dconf: default GSettings backend'
    'adwaita-icon-theme: default icon theme'
    'cantarell-fonts: default font'
    )
makedepends=(
    'git'
    'gcc-multilib'
    'gobject-introspection'
    'gtk-doc'
    )
source=(
    # GTK source code.
    "https://download.gnome.org/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
    # ArchLinux hook.
    'gtk-query-immodules-3.0-32.hook'
    # gtk3-mushrooms patches
    "git+$url.git#commit=$_commit"
)
sha256sums=('cbb16e4cfc928ab8f5f22f7290616f95f6ebc8c97cc724a2007b07ced833592b'
            'c87e56504cd38d73748d12e87353680156cdeef30d082cca407d2375db821115'
            'SKIP')

prepare() {
    cd gtk+-$pkgver
    $srcdir/$_repo/do-mushrooms.sh
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd gtk+-$pkgver

    export CC='gcc -m32'
    export CXX='/bin/false'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    CXX=/bin/false ./configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-x11-backend \
      --enable-wayland-backend \
      --disable-schemas-compile \
      --disable-gtk-doc-html \
      --disable-libcanberra

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd gtk+-$pkgver

    make DESTDIR="$pkgdir" install
    mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
    mv "${pkgdir}"/usr/bin/gtk-query-settings{,-32}
    rm "${pkgdir}"/usr/bin/{gtk-{builder-tool,encode-symbolic-svg,launch},gtk3-widget-factory}
    rm -rf "${pkgdir}"/{etc,usr/{include,share}}

    install -Dm 644 ../gtk-query-immodules-3.0-32.hook "${pkgdir}"/usr/share/libalpm/hooks/gtk-query-immodules-3.0-32.hook
}
