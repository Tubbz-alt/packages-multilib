# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Tomasz GÄ…sior <tomaszgasior.pl>

__arch_pkg_commit="712b5a879d9389efba706c8817607fa65ac562e9"
_repo=gtk3-mushrooms
_commit=d043f0d1d441b753677e9da8ef936331764c072a

pkgname=lib32-gtk3-classic
pkgver=3.22.21
pkgrel=3
pkgdesc="GTK3 library modified for classic GTK-based desktops."
url="https://github.com/TomaszGasior/gtk3-mushrooms"
conflicts=(lib32-gtk3)
provides=("lib32-gtk3=$pkgver")
arch=(x86_64)
license=(LGPL)
depends=(
    gtk3-classic
#    lib32-atk
    lib32-at-spi2-atk
#    lib32-cairo
    lib32-gdk-pixbuf2
    lib32-json-glib
    lib32-libcups
    lib32-libepoxy
    lib32-librsvg
    lib32-libxcomposite
    lib32-libxcursor
#    lib32-libxdamage
#    lib32-libxi
    lib32-libxinerama
    lib32-libxkbcommon
    lib32-libxrandr
#    lib32-mesa
    lib32-pango
    lib32-rest
#    lib32-wayland
    )
optdepends=(
    'gtk3-print-backends: printing'
    'dconf: default GSettings backend'
    'adwaita-icon-theme: default icon theme'
    'cantarell-fonts: default font'
    )
makedepends=(
    'git'
    'gcc-multilib'
    'gobject-introspection'
    'gtk-doc'
    )
source=(
    # GTK source code.
    "https://download.gnome.org/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
    # ArchLinux hook.
    'gtk-query-immodules-3.0-32.hook'
    # gtk3-mushrooms patches
    "git+$url.git#commit=$_commit"
)
sha256sums=('1bd3c1a85cfb4db112cabb5379abb05a1a94fe43052d309d573493fca00e6b87'
            'c87e56504cd38d73748d12e87353680156cdeef30d082cca407d2375db821115'
            'SKIP')

__patch_makefiles() {

    __replace_string_in_file() {
        sed -i".bak" "s/$1/$2/" "$3"
        [[ "$(cat "$3")" != "$(cat "$3.bak")" ]] || return 1
        rm "$3.bak"
    }

    __replace_string_in_file \
        "SRC_SUBDIRS = gdk gtk libgail-util modules demos tests testsuite examples" \
        "SRC_SUBDIRS = gdk gtk libgail-util modules demos" \
        "Makefile.am"

    __replace_string_in_file \
        "SUBDIRS = po po-properties \$(SRC_SUBDIRS) docs m4macros build" \
        "SUBDIRS = po \$(SRC_SUBDIRS) m4macros build" \
        "Makefile.am"

    __replace_string_in_file \
        "SUBDIRS = gtk-demo widget-factory icon-browser" \
        "SUBDIRS = widget-factory" \
        "demos/Makefile.am"

    __replace_string_in_file "SUBDIRS += cloudprint" "" "modules/printbackends/Makefile.am"
    __replace_string_in_file "SUBDIRS += cups" 	     "" "modules/printbackends/Makefile.am"
    __replace_string_in_file "gtk-update-icon-cache" "" "gtk/Makefile.am"
}

__patch_gtk_code() {
    for patchfile in $srcdir/$_repo/*.patch; do
        patch -d "gtk" -p 3 -i "$patchfile"
    done

    cat "$srcdir/$_repo/smaller-adwaita.css" >> "gtk/theme/Adwaita/gtk-contained.css"
    cat "$srcdir/$_repo/smaller-adwaita.css" >> "gtk/theme/Adwaita/gtk-contained-dark.css"
}

prepare() {
    cd gtk+-$pkgver

    # Make building faster by skipping tests, code examples and unused elements.
    __patch_makefiles

    # Apply patches to GTK code.
    __patch_gtk_code

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd gtk+-$pkgver

    export CC='gcc -m32'
    export CXX='/bin/false'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    CXX=/bin/false ./configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-x11-backend \
      --enable-wayland-backend \
      --disable-schemas-compile \
      --disable-gtk-doc-html \
      --disable-installed-tests \
      --disable-libcanberra \
      --disable-maintainer-mode

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd gtk+-$pkgver

    make DESTDIR="$pkgdir" install
    mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
    mv "${pkgdir}"/usr/bin/gtk-query-settings{,-32}
    rm "${pkgdir}"/usr/bin/{gtk-{builder-tool,encode-symbolic-svg,launch},gtk3-widget-factory}
    rm -rf "${pkgdir}"/{etc,usr/{include,share}}

    install -Dm 644 ../gtk-query-immodules-3.0-32.hook "${pkgdir}"/usr/share/libalpm/hooks/gtk-query-immodules-3.0-32.hook
}
